managed implementation in class zbp_r_event unique;
strict ( 2 );

define behavior for ZR_Event
  persistent table zce_event
  lock master
  authorization master ( global )
  etag master LocalLastChangedAt
{
  create ( authorization : global );
  update;
  delete;

  action openEvent;
  action closeEvent;

  field ( readonly, numbering : managed ) EventUuid;
  field ( readonly ) EventId, StatusText,
                     CreatedBy, CreatedAt, LastChangedBy, LastChangedAt, LocalLastChangedAt;

  field ( mandatory ) Title, Location, StartDate, EndDate, MaxParticipants, Description;

  determination initEventOnCreate on modify { create; }
  validation   validateEventDates   on save   { create; update; }

  association _Registrations { create; }

  mapping for zce_event
  {
    EventUuid         = event_uuid;
    EventId           = event_id;
    StatusText        = status;

    Title             = title;
    Location          = location;
    StartDate         = start_date;
    EndDate           = end_date;
    MaxParticipants   = max_participants;
    Description       = description;

    CreatedBy         = created_by;
    CreatedAt         = created_at;
    LastChangedBy     = last_changed_by;
    LastChangedAt     = last_changed_at;
    LocalLastChangedAt= local_last_changed_at;
  }
}

define behavior for ZR_REGISTRATION
persistent table zce_registration
lock dependent by _Event
authorization dependent by _Event
etag master LocalLastChangedAt
{
  update;
  delete;

  action ApproveRegistration;
  action RejectRegistration;

  validation   validateMaxParticipants   on save   { create; update; }

  field (readonly, numbering : managed) RegistrationUuid;
  field ( readonly ) RegistrationId, EventUuid, Status,
                     CreatedBy, CreatedAt, LastChangedBy, LastChangedAt, LocalLastChangedAt;

  field ( mandatory ) ParticipantUuid;

  determination determineInitValues on modify { create; }

  association _Event;
}